buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE"
    }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"

    testCompile "junit:junit"
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

sourceSets {
    test {
        java.srcDirs = ["src/test/unit/java"]
        resources.srcDirs = ["src/test/unit/resources"]
    }

    integration {
        java.srcDirs = ["src/test/intg/java"]
        resources.srcDirs = ["src/test/intg/resources"]

        compileClasspath += sourceSets.main.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.integration.java.srcDirs
        testSourceDirs += project.sourceSets.integration.resources.srcDirs
    }
}

task integration(type: Test) {
    description = "Run integration tests"
    group = "verification"

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath

    mustRunAfter test
}

//check.dependsOn integration

sourceCompatibility = 1.8
targetCompatibility = 1.8
